map "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaChEmedMedicationPrescriptionDocumentToBundle" = "CdaChEmedMedicationPrescriptionDocumentToFhirBundle"


// Medication Prescription document,	2.16.756.5.30.1.1.10.1.4
// 2020-01-22 Michaela Ziegler, copyright ahdis ag, Apache License 
// CDA-CH-EMED:  https://art-decor.org/art-decor/decor-project--cdachemed-
// FHIR CH-EMED: http://fhir.ch/ig/ch-emed/index.html, http://build.fhir.org/ig/hl7ch/ch-emed/index.html


uses "http://hl7.org/fhir/cda/StructureDefinition/ClinicalDocument" alias ClinicalDocument as source
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedAuthor" alias AssignedAuthor as source
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedEntity" alias AssignedEntity as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Author" alias Author as source
uses "http://hl7.org/fhir/cda/StructureDefinition/CustodianOrganization" alias CustodianOrganization as source
uses "http://hl7.org/fhir/cda/StructureDefinition/IVL_TS" alias IVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/EIVL_TS" alias EIVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Observation" alias Observation as source
uses "http://hl7.org/fhir/cda/StructureDefinition/PatientRole" alias PatientRole as source
uses "http://hl7.org/fhir/cda/StructureDefinition/RecordTarget" alias RecordTarget as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Section" alias Section as source
uses "http://hl7.org/fhir/cda/StructureDefinition/SubstanceAdministration" alias SubstanceAdministration as source
uses "http://hl7.org/fhir/cda/StructureDefinition/SXPR_TS" alias SXPR_TS as source

uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as target
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as produced
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as produced
uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as produced
uses "http://hl7.org/fhir/StructureDefinition/Organization" alias Organization as produced
uses "http://hl7.org/fhir/StructureDefinition/MedicationRequest" alias MedicationRequest as produced
uses "http://hl7.org/fhir/StructureDefinition/Dosage" alias Dosage as produced

imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaToFhirTypes"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaToBundle"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaChToBundle"

// _________________________ Document Level Template  _________________________ 

// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.1.4
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationprescription.html
group CdaChEmedMedicationPrescriptionDocumentToBundle(source cda : ClinicalDocument, target bundle : Bundle) {
  cda ->  bundle.entry as e share e, 
             e.resource = create('Composition') as composition share composition, 
             composition.id = uuid() as uuid,
             e.fullUrl = append('urn:uuid:',uuid),
             bundle.entry as e2, 
             e2.resource = create('Patient') as patient share patient,
             patient.id = uuid() as uuid2,
             e2.fullUrl = append('urn:uuid:',uuid2)
             then ClinicalDocumentChEmedMedicationPrescriptionDocumentToBundle(cda, patient, composition, bundle) "ClinicalDocumentToBody";
}

// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.1.4
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationprescription.html
group ClinicalDocumentChEmedMedicationPrescriptionDocumentToBundle(source cda : ClinicalDocument, target patient : Patient, target composition : Composition, target bundle : Bundle) extends ClinicalDocumentChToBundle {
  cda.component as component then {
    component.structuredBody as body then {
      body.component as component then {
        component.section as srcSection where (templateId.where(root='2.16.756.5.30.1.1.10.3.10')) -> composition.section as tgtSection then SectionPrescriptionSectionContentModule(srcSection, patient, tgtSection, bundle);
      } "component";
    } "body";
  } "component";
}

// _________________________ Section Level Templates _________________________ 

// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.3.10
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationprescription.html
group SectionPrescriptionSectionContentModule(source source : Section, source patient : Patient, target target, target bundle: Bundle) extends ClinicalDocumentSection {
  source.entry as cdaEntry ->  bundle.entry as e,  e.resource = create('MedicationRequest') as medicationrequest,  
    medicationrequest.id = uuid() as uuid,
    e.fullUrl = append('urn:uuid:',uuid), 
    target.entry = create('Reference') as reference, reference.reference = reference(medicationrequest) then {
    cdaEntry.substanceAdministration as substanceAdministration then PrescriptionItemEntryContentModule(source, substanceAdministration, patient, medicationrequest);
  };
}

// _________________________ Entry Level Templates   _________________________ 

// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.4.2
// target: Annotation note (e.g. http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-medicationrequest.html)
group AnnotationComment(source section: Section, source act: Act, target note: Annotation) {
  act -> note.text =  (%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%act.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
  act.text as text then {
    text.reference as reference then {
      reference.value as value -> note.extension as ext then NarrativeLink(value, ext) "narrativeLink";
    } "reference";
  } "text";
}


// source: Substitution permission Contains 1.3.6.1.4.1.19376.1.9.1.3.9.1 IHE Substitution Permission Content Module (DYNAMIC)
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-medicationrequest.html
group Substitution(source act: Act, target medicationRequest: MedicationRequest) {
  act -> medicationRequest.substitution as substitution then {
    
    //act.code as code -> substitution.allowed = create('CodeableConcept') as allowedCC then CECodeableConcept(code, allowedCC) "allowedCC";
    // workaround, because the line above generates no uri for the oid
    act.code as actCode -> substitution.allowed = create('CodeableConcept') as allowedCC, allowedCC.coding as coding then {
      actCode -> coding.system as systemCC, systemCC.value = 'http://terminology.hl7.org/CodeSystem/v3-substanceAdminSubstitution' "system";
      actCode.code as code -> coding.code as codeCC, codeCC.value = code "code";
      actCode.displayName as display -> coding.display as displayCC, displayCC.value = display "display";
    } "allowedCC";

  } "substitution";
}

// Number of packages
// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?id=2.16.756.5.30.1.1.10.4.38
// target: http://build.fhir.org/ig/hl7ch/ch-emed/branches/master/StructureDefinition-ch-emed-medicationrequest.html
group PrescribedQuantity(source supply: Supply, target medicationRequest: MedicationRequest) {
  supply.quantity as quantity -> medicationRequest.dispenseRequest as dispenseRequest, dispenseRequest.quantity as quant then {
    // IHE-PRE: If the product-element contains package information, the unit attribute is not be present
    quantity.value as value -> quant.value = value "value";

    // IHE-PRE: If the product-element does not contain package information, the unit attribut is present and the value SHALL be out of the UCUM code system
    quantity.unit as unit then {
      unit -> quant.unit = unit "unit";
      unit -> quant.system = 'http://unitsofmeasure.org' "ucum";
      unit -> quant.code = unit "code";
    } "unit";
  } "quantity";
}



// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.4.33
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-medicationrequest.html
group ManufacturedMaterialEntryContentModule(source source : SubstanceAdministration, target medicationRequest : MedicationRequest, target medication : Medication) {
  source -> medication.id = 'med', medicationRequest.medication = create('Reference') as vt, vt.reference = '#med' "medication";
  source.consumable as consumable then {
    consumable.manufacturedProduct as manufacturedProduct then {
      manufacturedProduct.manufacturedMaterial as manufacturedMaterial then {

        // #pre.no.brandedmedication
        manufacturedMaterial.code as code then {
          code.originalText as text then {
            text.reference as reference -> medication.code as medCode then {
              reference.value as value -> medCode.extension as ext then NarrativeLink(value, ext) "narrativeLink";
            } "reference";
          } "text";
        } "code";

        manufacturedMaterial.asContent as asContent then {
          asContent.containerPackagedMedicine as containerPackagedMedicine then {
            containerPackagedMedicine.code as code -> medication.code as fhircode, fhircode.text = (%manufacturedMaterial.name.other) then CECodeableConcept(code, fhircode) "medication.code"; 
            containerPackagedMedicine.formCode as formCode -> medication.form as form then CECodeableConcept(formCode, form)  "medication.formCode";
            
            // Package size
            containerPackagedMedicine.capacityQuantity as capacityQuantity where (%containerPackagedMedicine.formCode.code='10219000') -> medication.amount = create('Ratio') as ratio then {
              // IHE: Case 1: If the capacityQuantity is given in countable units, the unit attribute SHALL NOT be present
              capacityQuantity where $this.unit.exists()=false then {
                capacityQuantity.value as value -> ratio.numerator = create('Quantity') as quantity, 
                      quantity.value = value, 
                      ratio.denominator = create('Quantity') as denominator
                      , 
                      denominator.value = '1'
                      "capacityQuantity"; 
              } "no unit";

              // IHE: Case 2: If the capacityQuantity is given in non-countable units, the unit attribute SHALL be present and the value SHALL be out of the UCUM code system.
              capacityQuantity where $this.unit.exists() then {
                capacityQuantity.unit as unit then {
                  capacityQuantity.value as value -> ratio.numerator = create('Quantity') as quantity, 
                      quantity.value = value, 
                      quantity.unit = unit, 
                      quantity.system = 'http://unitsofmeasure.org',
                      quantity.code = unit,
                      ratio.denominator	 = create('Quantity') as denominator
                      , 
                      denominator.value = '1',
                      denominator.unit = 'Package',
                      denominator.system = 'http://unitsofmeasure.org',
                      denominator.code = '1'
                      "capacityQuantity"; 
                } "capQuantUnit";
              } "unit";
            } "ratio";

          } "containerPackagedMedicine";
        } "asContent";
        manufacturedMaterial.ingredient as ingredient -> medication.ingredient as ing then {
          ingredient.quantity as quantity -> ing.strength = create('Ratio') as strength then RTOPQPQRatio(quantity, strength) "strength";
          ingredient.ingredient as medingredient then {
              medingredient.code as code -> ing.item = create('CodeableConcept') as ingcode, ingcode.text = (%medingredient.name.other) then CECodeableConcept(code, ingcode) "ingredientcode";
          } "ingredient.ingredient";
        } "ingredient";
      } "manufacturedMaterial";
    } "manufacturedProduct";

    // dosage for normal dosing, as no sequences are present there
    source where $this.entryRelationship.sequenceNumber.exists()=false
      -> medicationRequest.dosageInstruction as dosage then DosageInstructionsStartStopFrequency(source, dosage) "dosage";

  } "consumable";
}


// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.4.43
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-medicationrequest.html
group PrescriptionItemEntryContentModule(source section : Section, source source : SubstanceAdministration, source patient : Patient, target medicationRequest : MedicationRequest) {
  
  source.id -> medicationRequest.identifier;
  patient -> medicationRequest.subject = create('Reference') as reference, reference.reference = reference(patient) "patient";
  source -> medicationRequest.status = 'completed' "completed";
  source -> medicationRequest.intent = 'order' "order";
  source.text as text then {
    text.reference as reference then {
      reference.value as value -> medicationRequest.extension as ext then NarrativeLink(value, ext) "narrativeLink";
    } "reference";
  } "text";
  source -> medicationRequest.contained = create('Medication') as medication then ManufacturedMaterialEntryContentModule(source, medicationRequest, medication) "medication";

  source.entryRelationship as entry where typeCode = 'RSON' then {
    entry.observation as observation 
      -> medicationRequest.reasonCode as reasonCode 
          then TreatmentReasonEntryContentModule(section, observation, reasonCode) "reasonCode";
  } "entryRelationShip 2.16.756.5.30.1.1.10.4.41";

  source.entryRelationship as entry where (typeCode='COMP' and substanceAdministration.templateId.root='2.16.756.5.30.1.1.10.4.37') 
    -> medicationRequest.dosageInstruction as dosage 
        then DosageInstructionsNonStructuredEntryContentModule(section, entry, dosage) "entryRelationShip 2.16.756.5.30.1.1.10.4.37";  

  // dosage for split dosing, as sequences are present there
  source.entryRelationship as entry where (typeCode='COMP' and sequenceNumber.value>=0) 
    -> medicationRequest.dosageInstruction as dosage 
        then DosageInstructionsEntryDosageChange(source, entry, dosage) "entryRelationShip 2.16.756.5.30.1.1.10.4.36";  
  
  source.entryRelationship as entry where (typeCode='REFR' and substanceAdministration.templateId.where(root='1.3.6.1.4.1.19376.1.9.1.3.10').exists()) 
    -> medicationRequest.extension as ext then MTPReferenceEntryContentModule(entry, ext) "MTPReferenceEntry";   

  source.entryRelationship as entry where (typeCode='COMP' and act.templateId.where(root='2.16.756.5.30.1.1.10.4.2').exists()) then {
    entry.act as act -> medicationRequest.note as note then AnnotationComment(section, act, note) "annotation";
  } "entryRelationShip 2.16.756.5.30.1.1.10.4.2";  

  source.repeatNumber as repeatNumber -> medicationRequest.dispenseRequest as dispenseRequest then {
    repeatNumber -> dispenseRequest.numberOfRepeatsAllowed = repeatNumber "repeatNumber";
  } "repeats";

  source.entryRelationship as entry where (typeCode='COMP' and supply.templateId.where(root='1.3.6.1.4.1.19376.1.9.1.3.8').exists()) then {
    entry.supply as supply -> medicationRequest as medicationRequest then PrescribedQuantity(supply, medicationRequest) "quantity";
  } "entryRelationShip 1.3.6.1.4.1.19376.1.9.1.3.8";

  source.entryRelationship as entry where (typeCode='COMP' and act.templateId.where(root='1.3.6.1.4.1.19376.1.9.1.3.9.1').exists()) then {
    entry.act as act -> medicationRequest as medicationRequest then Substitution(act, medicationRequest) "substitution";
  } "entryRelationShip 1.3.6.1.4.1.19376.1.9.1.3.9.1";
}


// source: https://art-decor.org/art-decor/decor-templates--ch-pharm-?id=2.16.756.5.30.1.1.10.4.35 
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition/ch-emed-dosage-structured
// dosage for normal dosing, without sequences
group DosageInstructionsStartStopFrequency(source source : SubstanceAdministration, target dosage: Dosage) {
  source.templateId as templateId where $this.root='1.3.6.1.4.1.19376.1.5.3.1.4.7.1' -> dosage.extension as ext then {
    source -> ext.url = 'http://fhir.ch/ig/ch-emed/StructureDefinition/ch-emed-ext-dosagetype' "url";
    source -> ext.value = create('Identifier') as valueIdentifier then {
      templateId.root as id -> valueIdentifier.value = id "normal dosing";
    } "valueIdentifier";
  } "extension";
  
  source as source -> dosage as dosage then EffectiveTimeStartEnd(source, dosage) "effective time start end";
  source as source -> dosage as dosage then EffectiveTimeWhen(source, dosage) "effective time when";
  source.routeCode as routeCode -> dosage.route as route then CECodeableConcept(routeCode, route) "routeCode";
  source as source -> dosage as dosage then DoseQuantity(source, dosage) "dose quantity";
}

// source https://art-decor.org/art-decor/decor-templates--ch-pharm-?id=2.16.756.5.30.1.1.10.4.36
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition/ch-emed-dosage-structured
// dosage for split dosing, with sequences
group DosageInstructionsEntryDosageChange(source source: substanceAdministration, source entry: entryRelationship, target dosage: Dosage) {
  source.templateId as templateId where (($this.root='1.3.6.1.4.1.19376.1.5.3.1.4.9') or ($this.root='1.3.6.1.4.1.19376.1.5.3.1.4.8'))
     -> dosage.extension as ext then {
        source -> ext.url = 'http://fhir.ch/ig/ch-emed/StructureDefinition/ch-emed-ext-dosagetype' "url";
        source -> ext.value = create('Identifier') as valueIdentifier then {
          templateId.root as id -> valueIdentifier.value = id "dosing type";
        } "valueIdentifier";
  } "extension";
  
  entry.sequenceNumber as sequenceNumber -> dosage.sequence = sequenceNumber "sequenceNumber";
  source as source -> dosage as dosage then EffectiveTimeStartEnd(source, dosage) "effective time start end";
  entry.substanceAdministration as source -> dosage as dosage then EffectiveTimeWhen(source, dosage) "effective time when";
  source.routeCode as routeCode -> dosage.route as route then CECodeableConcept(routeCode, route) "routeCode";
  entry.substanceAdministration as source -> dosage as dosage then DoseQuantity(source, dosage) "dose quantity";  
}

// effective time start & end for dosage
group EffectiveTimeStartEnd(source source: substanceAdministration, target dosage: Dosage) {
  source.effectiveTime: IVL_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime -> repeat.bounds = create('Period') as period then {
        effectiveTime.low as low then {
          low.value as lowDate -> period.start = lowDate "lowDate";
        } "low";
        effectiveTime.high as high then {
          high.value as highDate -> period.end = highDate "highDate";
        } "high";
      } "period";
    } "repeat";
  } "effectiveTime IVL_TS";
}

// effective time when for dosage
group EffectiveTimeWhen(source source: substanceAdministration, target dosage: Dosage) {
  source.effectiveTime: EIVL_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime.event as event then {
        event.code as code -> repeat.when = code "code";
      } "event";
    } "repeat";
  } "effectiveTime EIVL_TS";
  source.effectiveTime: SXPR_TS as effectiveTime -> dosage.timing as timing share sharetiming then {
    effectiveTime -> timing.repeat as repeat then {
      effectiveTime.comp as comp then {
        comp.event as event then {
          event.code as code -> repeat.when = code "code";
        } "event";
      } "comp";
    } "repeat";
  } "effectiveTime SXPR_TS";
}

// dose quantity for dosage (application schema)
group DoseQuantity (source source: substanceAdministration, target dosage: Dosage) {
  source.doseQuantity as doseQuantity -> dosage.doseAndRate as doseAndRate then {
    doseQuantity.center as center -> doseAndRate.dose = create('Quantity') as quantity then {
      center.value as value -> quantity.value = value "value";
    } "quantity";
  } "doseQuantity";
}


// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.4.41
// target: reasonCode Coding (e.g. http://build.fhir.org/ig/hl7ch/ig/ch-emed/StructureDefinition/ch-emed-medicationrequest)
group TreatmentReasonEntryContentModule(source section : Section, source observation: Observation, target reasonCode : Coding) {
    // extraxt text (Bluthochdruck) from id #mtpc.1.reason in section text <td ID="mtpc.1.reason">Bluthochdruck</td>
    //                                                                             a--------------b------------c
    // id = (%observation.text.reference.value.substring(1))
    // a = %section.text.substring(%section.text.indexOf(%id))
    // res = (%a.substring(%a.indexOf('>')+1, %a.indexOf('<')-%a.indexOf('>')-1))
    // a = %section.text.substring(%section.text.indexOf((%observation.text.reference.value.substring(1))))
    observation -> reasonCode.text =  (%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
    observation.text as text then {
      text.reference as reference then {
        reference.value as value -> reasonCode.extension as ext then NarrativeLink(value, ext) "narrativeLink";
      } "reference";
    } "text";
}


// source: https://art-decor.org/art-decor/decor-templates--ch-pharm-?section=templates&id=2.16.756.5.30.1.1.10.4.45
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-ext-treatmentplan.html
group MTPReferenceEntryContentModule(source entryrelationship, target ext: Extension) {
  entryrelationship -> ext.url = 'http://fhir.ch/ig/ch-emed/StructureDefinition/ch-emed-ext-treatmentplan' "url";
  entryrelationship.substanceAdministration as substanceAdministration then {
    substanceAdministration.id as id -> ext.extension as ext then InnerExtensionId(id, ext) "InnerExtensionId";
    substanceAdministration.reference as reference then {
      reference.externalDocument as externalDocument then {
        externalDocument.id as id -> ext.extension as ext then InnerExtensionExternalDocumentId(id, ext) "InnerExtensionExternalDocumentId";
      } "externalDocument";
    } "substanceAdministration";
  } "id";
}


// source: https://art-decor.org/art-decor/decor-templates--ch-pharm-?section=templates&id=2.16.756.5.30.1.1.10.4.52
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-dosage-nonstructured.html
group DosageInstructionsNonStructuredEntryContentModule(source section : Section, source entry : Element, target dosage : Dosage) {
  // see PrescriptionItemEntryContentModule
  entry.substanceAdministration as observation -> dosage.text = (%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).substring(%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')+1, %section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('<')-%section.text.substring(%section.text.indexOf(%observation.text.reference.value.substring(1))).indexOf('>')-1)) "idRef"; 
  entry.substanceAdministration as observation then {
    observation.text as text then {
      text.reference as reference then {
        reference.value as value -> dosage.extension as ext then NarrativeLink(value, ext) "narrativeLink";
      } "reference";
    } "text";
  } "observation";
}


// _________________________ Template Type not specified   _________________________ 

// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-ext-treatmentplan.html
group InnerExtensionExternalDocumentId(source source, target ext: Extension) {
  source -> ext.url = 'externalDocumentId' "url";
  source -> ext.value = create('Identifier') as id then II(source, id) "value";
}

// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-ext-treatmentplan.html
group InnerExtensionId(source source, target ext: Extension) {
  source -> ext.url = 'id' "url";
  source -> ext.value = create('Identifier') as id then II(source, id) "value";
}