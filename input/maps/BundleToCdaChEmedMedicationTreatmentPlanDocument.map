map "http://fhir.ch/ig/cda-fhir-maps/StructureMap/BundleToCdaChEmedMedicationTreatmentPlanDocument" = "BundleToCdaChEmedMedicationTreatmentPlanDocument"


// Medication Treatment Plan document, 2.16.756.5.30.1.1.10.1.7
// 2020-11-12 Michaela Ziegler, copyright ahdis ag, Apache License 
// CDA-CH-EMED:  https://art-decor.org/art-decor/decor-project--cdachemed-
// FHIR CH-EMED: http://fhir.ch/ig/ch-emed/index.html


uses "http://hl7.org/cda/stds/core/StructureDefinition/ClinicalDocument" alias ClinicalDocument as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/AssignedAuthor" alias AssignedAuthor as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/AssignedEntity" alias AssignedEntity as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/Author" alias Author as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/CustodianOrganization" alias CustodianOrganization as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/IVL-TS" alias IVL_TS as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/EIVL-TS" alias EIVL_TS as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/PatientRole" alias PatientRole as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/RecordTarget" alias RecordTarget as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/Section" alias Section as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/SubstanceAdministration" alias SubstanceAdministration as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/SXPR-TS" alias SXPR_TS as target
uses "http://hl7.org/cda/stds/core/StructureDefinition/Supply" alias Supply as target

uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as source
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as source
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as source
uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as source
uses "http://hl7.org/fhir/StructureDefinition/Organization" alias Organization as source
uses "http://hl7.org/fhir/StructureDefinition/MedicationStatement" alias MedicationStatement as source
uses "http://hl7.org/fhir/StructureDefinition/Dosage" alias Dosage as source


imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/FhirToCdaTypes"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/BundleToCda"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/BundleToCdaCh"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/BundleToCdaChEmed"


// _________________________ Document Level Template  _________________________ 

// source: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationtreatmentplan.html
// target: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.1.7
group BundleToCdaChEmedMedicationTreatmentPlanDocument(source bundle : Bundle, target cda : ClinicalDocument) {
    bundle -> cda.recordTarget as recordTarget, cda.author as author then BundleToClinicalDocumentEmedMedicationTreatmentPlanDocument(bundle, recordTarget, author, cda) "bundle";
}

// source: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationtreatmentplan.html
// target: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.1.7
group CompositionClinicalDocumentChEmedMedicationTreatmentPlanDocument(source bundle : Bundle, source composition : Composition, target cda : ClinicalDocument) {
  composition.type -> cda.code as code, code.codeSystem = '2.16.840.1.113883.6.1', code.codeSystemName='LOINC', code.code = '77603-9', code.displayName='Medication treatment plan.extended' "fixloinc";
  composition.section as section where $this.code.coding.where(system='http://loinc.org' and code='77604-7') -> cda.component as component then {
    section -> component.contextConductionInd = true "contextConductionInd";
    section -> component.structuredBody as structuredBody, structuredBody.component as component, component.section as cdasection then SectionMedicationTreatmentPlanSectionContentModule(bundle, section,cdasection) "cdasection";
  } "SectionMedicationTreatmentPlanSectionContentModule";
  composition.section as section where $this.code.coding.where(system='http://loinc.org' and code='48767-8') -> cda.component as component then {
    section -> component.contextConductionInd = true "contextConductionInd";
    section -> component.structuredBody as structuredBody, structuredBody.component as component, component.section as cdasection then SectionCdaRemarksCoded(bundle, section, cdasection) "cdasection";
  } "SectionRemarksCoded";
  composition.section as section where $this.code.coding.where(system='http://loinc.org' and code='55108-5') -> cda.component as component then {
    section -> component.contextConductionInd = true "contextConductionInd";
    section -> component.structuredBody as structuredBody, structuredBody.component as component, component.section as cdasection then SectionCdaOriginalRepresentation(bundle, section, cdasection) "cdasection";
  } "SectionCdaOriginalRepresentation";
}

// _________________________ Section Level Templates _________________________ 

// source: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationtreatmentplan.html
// target: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.3.13
group SectionMedicationTreatmentPlanSectionContentModule(source bundle : Bundle, source section: Section, target cdasection: CdaSection) extends SectionCdaSection{
  section -> cdasection.templateId as templateId, templateId.root='2.16.756.5.30.1.1.10.3.13' "CH-PHARM-MTP";
  section -> cdasection.templateId as templateId, templateId.root='1.3.6.1.4.1.19376.1.9.1.2.6' "IHE-PHARM-MTP";
  
  section where text.exists() = false -> cdasection.text = '<div xmlns="http://www.w3.org/1999/xhtml"><span id="refpdf">see pdf</span></div>' "Section";


  bundle.entry as entry then {
    entry.fullUrl where ($this in %section.entry.reference) and $this.startsWith('urn:uuid') then {
      entry.resource as medicationStatement where $this.ofType(FHIR.MedicationStatement)  -> cdasection.entry as entry, entry.substanceAdministration as substanceAdministration then MedicationTreatmentPlanItemEntryContentModule(bundle, medicationStatement, substanceAdministration) "MedicationStatement";
    } "fullUrlAsUuid";
    entry.resource as medicationStatement where $this.ofType(FHIR.MedicationStatement) and (('MedicationStatement'+'/'+$this.id) in %section.entry.reference) -> cdasection.entry as entry, entry.substanceAdministration as substanceAdministration then MedicationTreatmentPlanItemEntryContentModule(bundle, medicationStatement, substanceAdministration) "MedicationStatement";
  } "entry";
}


// _________________________ Header Level Templates _________________________ 

// source: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationtreatmentplan.html
// target: 2.16.756.5.30.1.1.10.9.44 
group BundleToClinicalDocumentEmedMedicationTreatmentPlanDocument(source bundle : Bundle, target recordTarget: RecordTarget, target author: Author, target cda : ClinicalDocument) extends BundleToClinicalDocumentCh {
  bundle -> cda.templateId as templateId, templateId.root = '2.16.756.5.30.1.127.1.4' "exchangeFormatAccordingToTheSwissEPR";
  bundle -> cda.templateId as templateId, templateId.root = '1.3.6.1.4.1.19376.1.5.3.1.1.1' "IHE-PCC";
  bundle -> cda.templateId as templateId, templateId.root = '1.3.6.1.4.1.19376.1.9.1.1.6' "IHE-PHARM-MTP";
  bundle -> cda.templateId as templateId, templateId.root = '2.16.756.5.30.1.1.10.1.7' "CDA-CH-EMED-MedicationTretmentPlanDocument";
  bundle.entry as entry then {
    entry.resource as resource where $this.ofType(FHIR.Composition) then CompositionClinicalDocumentChEmedMedicationTreatmentPlanDocument(bundle, resource, cda);
  } "entry";
}
 